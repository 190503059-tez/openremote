apply plugin: "java"
apply plugin: "application"

mainClassName = "org.openremote.ccs.Server"

dependencies {

    compile project(":container")

    compile "org.openremote:or-commons:0.1.0"
    compile "org.openremote:or-object-model:0.2.1"

    // TODO Replace with Jackson
    compile "org.json:json:20090211"
    compile "net.sf.flexjson:flexjson:$flexjsonVersion"

    compile "org.springframework.security:spring-security-core:$springVersion"
}

distributions {
    main {
        contents {
            from("conf") {
                into "conf"
            }
        }
    }
}

installDist {
    into "${buildDir}/install/$distributions.main.baseName"
}

import com.bmuschko.gradle.docker.tasks.image.*

task createDockerfile(type: Dockerfile) {
    destFile = project.file("$project.buildDir/install/Dockerfile")
    from "java:8"
    environmentVariable "WEBSERVER_DOCROOT", "webapp"
    environmentVariable "LOGGING_CONFIG_FILE", "conf/logging.properties"
    addFile project(':beehive:ccs').distributions.main.baseName, "/opt/app"
    exposePort 8080
    workingDir "/opt/app"
    entryPoint "/opt/app/bin/${project(":beehive:ccs").name}"
}

task buildImage(type: DockerBuildImage) {
    dependsOn ":beehive:ccs:installDist", createDockerfile
    inputDir = projectDir
    tag = dockerImageName
}

task pushImage(type: DockerPushImage) {
    dependsOn buildImage
    imageName = dockerImageName
}
