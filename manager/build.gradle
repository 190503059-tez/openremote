evaluationDependsOnChildren()

import com.bmuschko.gradle.docker.tasks.image.*

task createDockerfile(type: Dockerfile) {
    destFile = project.file("$project.buildDir/install/Dockerfile")
    from "java:8"
    environmentVariable "WEBSERVER_DOCROOT", "webapp"
    environmentVariable "LOGGING_CONFIG_FILE", "config/logging.properties"
    environmentVariable "MAP_TILES_PATH", "config/mapdata.mbtiles"
    environmentVariable "MAP_SETTINGS_PATH", "config/mapsettings.json"
    addFile project(':manager:server').distributions.main.baseName, "/opt/app"
    addFile project(':manager:client').distributions.main.baseName, "/opt/app"
    exposePort 8080
    workingDir "/opt/app"
    entryPoint "/opt/app/bin/${project(":manager:server").name}"
}

task buildImage(type: DockerBuildImage) {
    dependsOn ":manager:server:installDist", ":manager:client:installDist", createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = dockerImageName
}

task pushImage(type: DockerPushImage) {
    dependsOn buildImage
    imageName = dockerImageName
}
