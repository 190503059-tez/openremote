buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "de.richsource.gradle.plugins:gwt-gradle-plugin:$gwtGradlePluginVersion"
        classpath "net.ltgt.gradle:gradle-apt-plugin:$aptGradlePluginVersion"
        classpath "com.bmuschko:gradle-docker-plugin:$dockerGradlePluginVersion"
    }
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "net.ltgt.apt"
apply plugin: "gwt-compiler"
apply plugin: "com.bmuschko.docker-remote-api"

mainClassName = "org.openremote.manager.server.Server"

dependencies {

    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:slf4j-jdk14:$slf4jVersion"
    compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    compile "org.slf4j:log4j-over-slf4j:$slf4jVersion"

    compile "io.vertx:vertx-web:$vertxVersion"
    compile "com.yahoo.elide:elide-core:1.0.0.20"
    compile "com.yahoo.elide:elide-datastore-hibernate5:1.0.0.20"

    compile "com.h2database:h2:$h2Version"
    compile "com.zaxxer:HikariCP:$hikaricpVersion"
    compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
    compile "org.hibernate:hibernate-java8:$hibernateVersion"
    compile "org.hibernate:hibernate-validator:$hibernateValidatorVersion"

    compile "com.google.gwt:gwt-user:$gwtVersion"
    compile "com.google.gwt:gwt-elemental:$gwtVersion"
    compile ("com.google.gwt.inject:gin:$ginVersion") {
        exclude group: "asm"
    }
    compile "org.fusesource.restygwt:restygwt:$restygwtVersion"

    apt "org.hibernate:hibernate-jpamodelgen:$hibernateVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "io.vertx:vertx-unit:$vertxVersion"

}

gwt {
    gwtVersion = project.properties.gwtVersion

    sourceLevel = JavaVersion.VERSION_1_8
    maxHeapSize = "1024M"

    modules("org.openremote.manager.Manager")

    test {
        hasGwtTests = false
    }

    compiler {
        style = "PRETTY"
    }

    superDev {

        // Generate minimal JS launcher in the webapp source folder (you might commit this or not, doesn't matter)
        launcherDir = file("src/main/webapp/gwt")

        // Careful, this binds to all interfaces on your machine!
        bindAddress = "0.0.0.0"
    }
}

// TODO Why is this not configurable?
tasks.withType(de.richsource.gradle.plugins.gwt.AbstractGwtActionTask) {

    doFirst {
        mkdir file("src/main/webapp/gwt")
    }

    //args "-logLevel", "TRACE"
    args "-generateJsInteropExports"
    args "-noincremental" // TODO Yep, incremental compile is apparently broken right now in 2.8.0

    // I've had various problems with this cache and GWT snapshot updates, so it's a good idea to make it easily cleanable
    jvmArgs("-Dgwt.persistentunitcachedir=$project.buildDir/gwt/cache")
}

distributions {
    main {
        contents {
            from("src/main/webapp") {
                into "webapp"
                exclude "gwt"
            }
            from(compileGwt.outputs) {
                into "webapp/gwt"
                exclude "WEB-INF"
            }
        }
    }
}

import com.bmuschko.gradle.docker.tasks.image.*
import com.bmuschko.gradle.docker.tasks.container.*

docker {
    url = "$System.env.DOCKER_HOST".replace("tcp:", "https:")
    certPath = new File("$System.env.DOCKER_CERT_PATH")
}

task createDockerfile(type: Dockerfile) {
    destFile = project.file("build/install/Dockerfile")
    from "java:8"
    environmentVariable "WEB_SERVER_DOCROOT", "webapp"
    addFile project.name, "/opt/app"
    exposePort 8080
    workingDir "/opt/app"
    entryPoint "/opt/app/bin/$project.name"
}

task buildImage(type: DockerBuildImage) {
    dependsOn installDist, createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = dockerImageName
}

task createContainer(type: DockerCreateContainer) {
    dependsOn buildImage
    targetImageId { dockerImageName }
    portBindings = ['8080:8080']
    containerName = dockerContainerName
}

task startContainer(type: DockerStartContainer) {
    dependsOn createContainer
    targetContainerId { dockerContainerName }
}

task stopContainer(type: DockerStopContainer) {
    targetContainerId { dockerContainerName }
}

task removeContainer(type: DockerRemoveContainer) {
    targetContainerId { dockerContainerName }
}

task removeImage(type: DockerRemoveImage) {
    imageId = dockerImageName
}
