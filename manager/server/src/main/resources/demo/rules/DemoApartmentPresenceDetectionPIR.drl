package org.openremote.test.rules;

import org.openremote.model.*;
import org.openremote.model.asset.*
import org.openremote.model.value.Values
import java.util.Date
import org.openremote.model.attribute.AttributeEvent;

global java.util.logging.Logger LOG;
global org.openremote.model.rules.Assets assets;

/*
Calculate and set the "presenceDetected" flag and the "lastPresenceDetected" timestamp attributes
of a ROOM when a motion sensor triggers several times within a time window. The flag will be
removed if the motion sensor has not been triggered within a time window. All time windows and
the number of matches required can be configured.
*/

declare initFact
    @role(event)
    init: Boolean
end

rule "Insert init fact at startup"
then
    insert(new initFact(true));
end

rule "Init firstPresenceDetected"
salience 10
when
    $init: initFact(init)
    $room : AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    AssetState(id==$room.id, attributeName == "firstPresenceDetected")
then
    retract($init);
    assets.dispatch(
            new AttributeEvent($room.getId(), "firstPresenceDetected", Values.create(-1))
    );
end

rule "Fetch the first presence detected time stamp"
when
    // A room has a motion sensor
    $room : AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    // And the presence detected flag is not set already
    // TODO although rule "Init firstPresenceDetected" is triggered this does not work here when uncommented.
    // we need some kind on unset value marker, probably -1 does not work. In the old rules files it was string "-"
    // AssetState(id == $room.id, attributeName == "firstPresenceDetected", valueAsNumber==-1)
    AssetState(id == $room.id, attributeName == "firstPresenceDetected", value==null)
    // And the motion sensor is triggered
    $sensorState: AssetState(id == $room.id, attributeName == "motionSensor", valueAsBoolean == true)
then
    LOG.info("@-@"+(new java.util.Date(drools.getWorkingMemory().getSessionClock().getCurrentTime())).toString());
    // Fetch the first presence detected timestamp of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "firstPresenceDetected", Values.create($sensorState.getValueTimestamp()))
    );
end

declare CO2Rising
	@role(event)
	@expires(10m)
    roomId: String
end

rule "Insert CO2 rising event"
when
    AssetState($roomId: id, type==AssetType.ROOM, attributeName == "co2Level", valueAsNumber > Values.getNumber(oldValue).orElse(null))
then
    LOG.info("@-@"+(new java.util.Date(drools.getWorkingMemory().getSessionClock().getCurrentTime())).toString());
	insert(new CO2Rising($roomId));
end

rule "Set presence detected flag if motion sensor is triggered in time window"
when
    // A room has a motion sensor
    $room : AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    // And the presence detected flag is not set already
    AssetState(id == $room.id, attributeName == "presenceDetected", valueAsBoolean != true)
    // And at least 5 motion sensor triggers have been received in the last 10 minutes
    accumulate(
        AssetEvent(id == $room.id, attributeName == "motionSensor", valueAsBoolean == true) over window:time(10m);
        $count: count(1);
        $count >= 5
    )
    // Only when there is no CO2 sensor
    not AssetState(id == $room.id, attributeName == "co2Level")
then
    LOG.info("@-@"+(new java.util.Date(drools.getWorkingMemory().getSessionClock().getCurrentTime())).toString());
    // Set the presence detected flag of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "presenceDetected", Values.create(true))
    );
end

rule "Set presence detected flag if CO2 rises in time window"
when
    // A room has a motion sensor
    $room : AssetState(type == AssetType.ROOM, attributeName == "co2Level")
    // And "firstPresenceDetected" is true
//    AssetState(id == $room.id, attributeName == "firstPresenceDetected", valueAsNumber > 0)
    // And the presence detected flag is not set already
    AssetState(id == $room.id, attributeName == "presenceDetected", valueAsBoolean != true)
    // And at least 5 CO2Rising have been received in the last 10 minutes
    accumulate(
        CO2Rising(roomId==$room.id) over window:time(10m);
        $count: count(1);
        $count >= 5
    )
then
    LOG.info("@-@"+(new java.util.Date(drools.getWorkingMemory().getSessionClock().getCurrentTime())).toString());
    // Set the presence detected flag of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "presenceDetected", Values.create(true))
    );
end

rule "Update the presence detected timestamp if motion sensor is triggered and we have already detected presence"
when
    // The presence detected flag is set in a room
    $room: AssetState(type == AssetType.ROOM, attributeName == "presenceDetected", valueAsBoolean == true)
    // And the motion sensor is triggered
    $sensorState: AssetState(id == $room.id, attributeName == "motionSensor", valueAsBoolean == true)
then
    LOG.info("@-@"+(new java.util.Date(drools.getWorkingMemory().getSessionClock().getCurrentTime())).toString());
    // Update the last presence detected timestamp of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "lastPresenceDetected", Values.create($sensorState.getValueTimestamp()))
    );
end

rule "Clear presence detected flag if motion sensor was not triggered for 10m and CO2 is not rising for 20m"
when
    // A room has a motion sensor
    $room: AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    // And the presence detected flag is set
    AssetState(id == $room.id, attributeName == "presenceDetected", valueAsBoolean == true)
    // And no motion sensor triggers have been received in the last 10 minutes
    accumulate(
        AssetEvent(id == $room.id, attributeName == "motionSensor", valueAsBoolean == true) over window:time(10m);
        $count: count(1);
        $count == 0
    )
    // and no increasing CO2 in the last 20 minutes
    accumulate(
        CO2Rising(roomId == $room.id) over window:time(20m);
        $countCO2: count(1);
        $countCO2 == 0
    )
then
    // TODO this should be 10 min after the Update rule, but it happens at irregular times, sometimes earlier, sometimes later.
    LOG.info("@-@"+(new java.util.Date(drools.getWorkingMemory().getSessionClock().getCurrentTime())).toString());
    // Clear the presence detected flag of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "presenceDetected", Values.create(false))
    );
end
