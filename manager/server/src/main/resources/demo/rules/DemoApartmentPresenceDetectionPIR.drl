package org.openremote.test.rules;

import org.openremote.model.*;
import org.openremote.model.asset.*
import elemental.json.Json
import java.util.Date;

global java.util.logging.Logger LOG;
global org.openremote.model.rules.Assets assets;

/*
Calculate and set the "presenceDetected" flag and the "lastPresenceDetected" timestamp attributes
of a ROOM when a motion sensor triggers several times within a time window. The flag will be
removed if the motion sensor has not been triggered within a time window. All time windows and
the number of matches required can be configured.
*/

rule "Set presence detected flag if motion sensor is triggered in time window"
when
    // A room has a motion sensor
    $room : AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    // And at least 5 motion sensor triggers have been received in the last 10 minutes
    accumulate(
        AssetEvent(id == $room.id, attributeName == "motionSensor", value.asBoolean) over window:time(10m);
        $count: count(1);
        $count >= 5
    )
    // And the presence detected flag is not set already
    AssetState(id == $room.id, attributeName == "presenceDetected", !value.asBoolean)
then
    // Set the presence detected flag of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "presenceDetected", Json.create(true))
    );
end

rule "Update the presence detected timestamp if motion sensor is triggered and we have already detected presence"
when
    // The presence detected flag is set in a room
    $room: AssetState(type == AssetType.ROOM, attributeName == "presenceDetected", value.asBoolean)
    // And the motion sensor is triggered
    $sensor: AssetState(id == $room.id, attributeName == "motionSensor", value.asBoolean)
then
    // Update the last presence detected timestamp of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "lastPresenceDetected", Json.create($sensor.getValueTimestamp()))
    );
end

rule "Clear presence detected flag if motion sensor was not triggered in a while"
when
    // A room has a motion sensor
    $room: AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    // And the presence detected flag is set
    AssetState(id == $room.id, attributeName == "presenceDetected", value.asBoolean)
    // And no motion sensor triggers have been received in the last 10 minutes
    accumulate(
        AssetEvent(id == $room.id, attributeName == "motionSensor", value.asBoolean) over window:time(10m);
        $count: count(1);
        $count == 0
    )
then
    // Clear the presence detected flag of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "presenceDetected", Json.create(false))
    );
end
