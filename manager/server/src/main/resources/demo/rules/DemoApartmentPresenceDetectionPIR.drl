package org.openremote.test.rules;

import org.openremote.model.*;
import org.openremote.model.asset.*
import org.openremote.model.value.Values
import java.util.Date
import org.openremote.model.attribute.AttributeEvent;

global java.util.logging.Logger LOG;
global org.openremote.model.rules.Assets assets;

/*
Calculate and set the "presenceDetected" flag and the "lastPresenceDetected" timestamp attributes
of a ROOM when a motion sensor triggers several times within a time window. The flag will be
removed if the motion sensor has not been triggered within a time window. All time windows and
the number of matches required can be configured.
*/

rule "Set presence detected flag if motion sensor is triggered in time window"
when
    // A room has a motion sensor
    $room : AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    // And the presence detected flag is not set already
    AssetState(id == $room.id, attributeName == "presenceDetected", !valueAsBoolean)
    // And at least 5 motion sensor triggers have been received in the last 10 minutes
    accumulate(
        AssetEvent(id == $room.id, attributeName == "motionSensor", valueAsBoolean) over window:time(10m);
        $count: count(1);
        $count >= 5
    )
then
    // Set the presence detected flag of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "presenceDetected", Values.create(true))
    );
end

rule "Update the presence detected timestamp if motion sensor is triggered and we have already detected presence"
when
    // The presence detected flag is set in a room
    $room: AssetState(type == AssetType.ROOM, attributeName == "presenceDetected", valueAsBoolean)
    // And the motion sensor is triggered
    $sensorState: AssetState(id == $room.id, attributeName == "motionSensor", valueAsBoolean)
then
    // Update the last presence detected timestamp of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "lastPresenceDetected", Values.create($sensorState.getValueTimestamp()))
    );
end

rule "Clear presence detected flag if motion sensor was not triggered in a while"
when
    // A room has a motion sensor
    $room: AssetState(type == AssetType.ROOM, attributeName == "motionSensor")
    // And the presence detected flag is set
    AssetState(id == $room.id, attributeName == "presenceDetected", valueAsBoolean)
    // And no motion sensor triggers have been received in the last 10 minutes
    accumulate(
        AssetEvent(id == $room.id, attributeName == "motionSensor", valueAsBoolean) over window:time(10m);
        $count: count(1);
        $count == 0
    )
then
    // Clear the presence detected flag of the room
    assets.dispatch(
            new AttributeEvent($room.getId(), "presenceDetected", Values.create(false))
    );
end
